# 6. pipe演算子とEnumモジュール

## はじめに

Elixirでは関数型言語という特性を活かす演算子として、pipe演算子`|>`が導入されている。

これは複数の関数を連結する演算子である。

pipe演算子を用いることで、データを処理する一連のパイプラインを構築し、大きな処理を実現することができる。

pipe演算子に関連して、Listのような列挙可能(Enumerable)な値の処理に頻繁に用いられるのが`Enum`モジュールである。

`Enum`モジュールは「反復処理」や「集約」を抽象化する高階関数を提供しており、プログラマは処理の本質を関数で表現することに集中できる。

## pipe演算子

MyMathモジュールを用いて、次の処理を行う。

1. 1 + 2を行う
2. 1.の結果を4倍する
3. 2.の結果に10を足す

```elixir
defmodule MyMath do
  def add(x, y) do
    x + y
  end

  def multiple(x, y) do
    x * y
  end
end
```

```elixir
# 愚直に書くと返り値を毎回変数に束縛するか、次のように関数をネストして適用する必要がある
MyMath.add(MyMath.multiple(MyMath.add(1, 2), 4),10)
```

pipe演算子を使うと、前の関数の返り値が次の関数の第一引数として自動的に渡される。

結果として、関数を適用順に直列に書くことができる。

```elixir
MyMath.add(1, 2) |> MyMath.multiple(4) |> MyMath.add(10) 
# もしくは下記のように書くことができる
1 |> MyMath.add(2) |> MyMath.multiple(4) |> MyMath.add(10) 
```

途中の結果を出力したい場合は下記のように繋いでも良い

```elixir
MyMath.add(1, 2) |> IO.inspect() |> MyMath.multiple(4) |> IO.inspect() |> MyMath.add(10) 
```

`case`も関数なのでpipe演算子でつなぐことができる。

```elixir
File.read("resources/2/hello.txt")
|> case do
  {:ok, content} -> content
  {:error, msg} -> msg
end
|> IO.puts()
```

### 練習問題 Exercise 6-1

先ほどのMyMathモジュールを用いて、次の処理を行ってみよう。
その際、pipe演算子（`|>`）で関数を繋いで書くようにしてください。

1. 5 * 20を行う
2. 1.の結果を3倍する
3. 2.の結果に1000を足す

```elixir
result = # implement me!

IO.inspect(resulet)
```

## Enumモジュール

詳細は[公式ドキュメント](https://hexdocs.pm/elixir/Enum.html)を参照のこと。

ここでは一部の例を紹介するにとどめる。

### Enum.map/2

写像を行う関数である。

第一引数にEnumerable、第二引数に、1つの値をとって何らかの値を返す関数を取る。

```elixir
# 与えられた複数の整数に対し、それぞれ2倍する
# どのように繰り返し処理を行うかは本質ではない
# Enum モジュールの関数により、対象と適用する処理とを宣言的に記述できる

Enum.map([0, 1, 2, 3, 4], fn x -> x * 2 end)
```

```elixir
collection = [
  %{x: 0, y: 1},
  %{x: 2, y: 1},
  %{x: 4, y: 2}
]

Enum.map(collection, fn map -> map.x end)
```

```elixir
# もちろんpipe演算子で処理をつなげることができる

[0, 1, 2, 3, 4]
|> Enum.map(fn x -> x * 2 end)
|> Enum.map(fn x -> x + 1 end)
|> Enum.map(fn x -> "number #{x}" end)
```

### Enum.filter/2

第一引数にEnumerable、第二引数に、1つの値をとってbooleanを返す関数を取る。

第二引数の関数の返り値が`true`となる要素だけを残す。

```elixir
is_even? = &(Integer.mod(&1, 2) == 0)

[0, 1, 2, 3, 4]
|> Enum.filter(is_even?)
```
